# Common functions that use the tfplan import

##### Imports #####
import "tfplan/v2" as tfplan
import "strings"
import "types"

##### Functions #####

### find_resources ###
# Find all resources of a specific type from all modules using the tfplan import
# Only include resources that are being created or updated
find_resources = func(type) {
  resources = filter tfplan.resource_changes as address, rc {
  	rc.type is type and
  	rc.mode is "managed" and
  	(rc.change.actions contains "create" or rc.change.actions contains "update")
  }

  return resources
}

### find_datasources ###
# Find all data sources of a specific type from all modules using the tfplan import
# Only include data sources that are being created or updated
find_datasources = func(type) {
  resources = filter tfplan.resource_changes as address, rc {
  	rc.type is type and
  	rc.mode is "data" and
  	(rc.change.actions contains "create" or rc.change.actions contains "update")
  }

  return resources
}

### evaluate_attribute ###
# Evaluates the value of a resource's attribute even if nested
# The resource should be given in the initial call in the form
# "rc.change.after" where rc is a resource change derived by
# applying filters to tfplan.resource_changes
# Indices of lists should be given as 0, 1, 2, and so on
# For example: boot_disk.0.initialize_params.0.image
evaluate_attribute = func(r, attribute) {

  # Split the attribute into a list, using "." as the separator
  attributes = strings.split(attribute, ".")

  # Convert numeric strings to integers for indices
  if attributes[0] matches "^[0-9]+$" {
    a = int(attributes[0])
    # Make sure r is of type list
    if types.type_of(r) is not "list" {
      return undefined
    }
  } else {
    a = attributes[0]
  }

  # Append the current attribute to the resource instance
  new_r = r[a] else null

  # Validate that new_r exists
  if types.type_of(new_r) in ["map", "list"] and length(new_r) is 0  {
    return undefined
  }

  # Process based on length of attributes
  # being greater than or equal to 1
  if length(attributes) > 1 {

    # Strip first element from attributes
    attributes = attributes[1:length(attributes)]
    attribute = strings.join(attributes, ".")

    # Make recursive call
    return evaluate_attribute(new_r, attribute)
  } else {

    # We reached the end of the attribute and can stop the
    # recursive calls and return the value of the attribute
    return new_r

  }
}

### filter_attribute_not_in_list ###
# Filter a list of resources to those with a specified
# attribute (attr) that is not in a given list of allowed values (allowed).
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
# If you want to allow computed values, include null in the list, allowed.
filter_attribute_not_in_list = func(resources, attr, allowed) {
  violators = {}
  for resources as address, rc {
    # Evaluate the value (v) of the attribute
    v = evaluate_attribute(rc.change.after, attr) else null
    # Check if the value is not in the allowed list
    if v not in allowed {
      # If not, add it to the violators list and print a warning
      violators[address] = rc
      print(address, "has", attr, v, "that is not in the allowed list", allowed)
    }
  }
  return violators
}

### filter_attribute_in_list ###
# Filter a list of resources to those with a specified
# attribute (attr) that is in a given list of allowed values (disallowed).
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
# If you want to disallow computed values, include null in the list, disallowed.
filter_attribute_in_list = func(resources, attr, disallowed) {
  violators = {}
  for resources as address, rc {
    # Evaluate the value (v) of the attribute
    v = evaluate_attribute(rc.change.after, attr) else null
    # Check if the value is in the allowed list
    if v in disallowed {
      # If so, add it to the violators list and print a warning
      violators[address] = rc
      print(address, "has", attr, v, "that is in the disallowed list", disallowed)
    }
  }
  return violators
}

### filter_attribute_not_contains_list ###
# Filter a list of resources to those with a specified
# attribute (attr) that does not contain a given list of allowed values (required).
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_not_contains_list = func(resources, attr, required) {
  violators = {}
  for resources as address, rc {
    # Evaluate the value (v) of the attribute
    v = evaluate_attribute(rc.change.after, attr) else null
    # Check if the value contains the desired allowed list
    if v is null or
       not (types.type_of(v) is "list" or types.type_of(v) is "map") {
      # Add it to the violators list and print a warning
      # If you want to allow computed values, comment out the next line
      violators[address] = rc
      print(address, "has", attr, v, "that is missing or is not a map or a list")
    } else {
      for required as rv {
        if v not contains rv {
          violators[address] = rc
          print(address, "has", attr, v, "that is missing the required item",
          rv, "from the list:", required)
        } // end rv
      } // end for
    } // end else
  }
  return violators
}

### filter_attribute_contains_items_from_list ###
# Filter a list of resources to those with a specified
# attribute (attr) that contains any items from a given list of forbidden values.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_contains_items_from_list = func(resources, attr, forbidden) {
  violators = {}
  for resources as address, rc {
    # Evaluate the value (v) of the attribute
    v = evaluate_attribute(rc.change.after, attr) else null
    # Check if the value contains the desired allowed list
    if v is null or
       not (types.type_of(v) is "list" or types.type_of(v) is "map") {
      # Add it to the violators list and print a warning
      # If you want to allow computed values, comment out the next line
      violators[address] = rc
      print(address, "has", attr, v, "that is missing or is not a map or a list")
    } else {
      for forbidden as fv {
        if v contains fv {
          violators[address] = rc
          print(address, "has", attr, v, "with item", fv,
          "from the forbidden list:", forbidden)
        } // end rv
      } // end for
    } // end else
  }
  return violators
}

### filter_attribute_is_not_value ###
# Filter a list of resources to those with a specified
# attribute (attr) that does not have a given value.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_is_not_value = func(resources, attr, value) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr)
    if v else null is null {
      # Comment out next line to exclude undefined and null,
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if v is not value {
      violators[address] = rc
      print(address, "has", attr, v, "that is not equal to", value)
    }
  }
  return violators
}

### filter_attribute_is_value ###
# Filter a list of resources to those with a specified
# attribute (attr) that has a given value.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_is_value = func(resources, attr, value) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr)
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if v is value {
      violators[address] = rc
      print(address, "has", attr, v, "that is equal to", value)
    }
  }
  return violators
}

### filter_attribute_greater_than_value ###
# Filter a list of resources to those with a specified
# attribute (attr) that is greater than a given numeric value.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_greater_than_value = func(resources, attr, value) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if float(v) else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if float(v) > value {
      violators[address] = rc
      print(address, "has", attr, v, "that is greater than", value)
    }
  }
  return violators
}

### filter_attribute_less_than_value ###
# Filter a list of resources to those with a specified
# attribute (attr) that is less than a given numeric value.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_less_than_value = func(resources, attr, value) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if float(v) else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if float(v) < value {
      violators[address] = rc
      print(address, "has", attr, v, "that is less than", value)
    }
  }
  return violators
}

### filter_attribute_does_not_match_regex ###
# Filter a list of resources to those with a specified
# attribute (attr) that does not match a regular expression (expr).
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_does_not_match_regex = func(resources, attr, expr) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if v not matches expr {
      violators[address] = rc
      print(address, "has", attr, v, "that does not match the regex", expr)
    }
  }
  return violators
}

### filter_attribute_matches_regex ###
# Filter a list of resources to those with a specified
# attribute (attr) that matches a regular expression (expr).
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_matches_regex = func(resources, attr, expr) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if v matches expr {
      violators[address] = rc
      print(address, "has", attr, v, "that matches the regex", expr)
    }
  }
  return violators
}

### filter_attribute_does_not_have_prefix ###
# Filter a list of resources to those with a specified
# attribute (attr) that does not have a given prefix.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_does_not_have_prefix = func(resources, attr, prefix) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if not strings.has_prefix(v, prefix) {
      violators[address] = rc
      print(address, "has", attr, v, "that does not have the prefix", prefix)
    }
  }
  return violators
}

### filter_attribute_has_prefix ###
# Filter a list of resources to those with a specified
# attribute (attr) that has a given prefix.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_has_prefix = func(resources, attr, prefix) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if strings.has_prefix(v, prefix) {
      violators[address] = rc
      print(address, "has", attr, v, "that has the prefix", prefix)
    }
  }
  return violators
}

### filter_attribute_does_not_have_suffix ###
# Filter a list of resources to those with a specified
# attribute (attr) that does not have a given suffix.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_does_not_have_suffix = func(resources, attr, suffix) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if not strings.has_suffix(v, suffix) {
      violators[address] = rc
      print(address, "has", attr, v, "that does not have the suffix", suffix)
    }
  }
  return violators
}

### filter_attribute_has_suffix ###
# Filter a list of resources to those with a specified
# attribute (attr) that has a given suffix.
# Resources should be derived by applying filters to tfplan.resource_changes
# Prints warnings for all violations
filter_attribute_has_suffix = func(resources, attr, suffix) {
  violators = {}
  for resources as address, rc {
    v = evaluate_attribute(rc.change.after, attr) else null
    if v else null is null {
      # Comment out next line to exclude undefined and null values
      # from filtered list, thereby allowing computed values
      violators[address] = rc
      print(address, "does not have", attr, "defined")
    } else if strings.has_suffix(v, suffix) {
      violators[address] = rc
      print(address, "has", attr, v, "that has the suffix", suffix)
    }
  }
  return violators
}
